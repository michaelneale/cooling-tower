#
# This contains the rules for assigning apps to instances. 
#
# General principles:
# 
# If an application request has no preferred values, defaults should be set.
# If there is a vacant Instance which is suitable, use it up.
# If the application can or should not be colocated, then request a new instance for it.
# If there is a suitable existing server, with less then N apps on it, and you can colocate, do it
# If there is no suitable colo server, request a new one
#
# Apps should be allocated in a balanced fashion where possible.
#
package jboss.cloud
dialect "mvel"
global java.util.List results



#TIP: can lock this down to specific image types
rule "Always prefer empty server"
    salience 100
    when
        application: Application()
        instance: Instance(numberOfApps == 0,
                            spareMemory >= application.memory,
                            spareStorage >= application.disk)
   then
        insert(new Assignment(application, instance))

end

rule "Can't colocate - so need a new server"
    when
        application: Application(canCoExist == false)
        not Instance(numberOfApps == 0,
                     spareMemory >= application.memory,
                     spareStorage >= application.disk)
    then
        insert(new NewInstanceNeeded(application))
end

#
#TIP: you can lock it down to specific image types here if you like...
#
rule "Can colocate - see if we can fit it in somewhere"
    when
        application: Application(canCoExist == true)
        instance: Instance(numberOfApps < 4,
                            spareMemory >= (application.memory * 1.2),
                            spareStorage >= (application.disk * 1.5) )
        #we want to make sure there is no other instance with less apps !                            
        not Instance(numberOfApps < instance.numberOfApps,
                     spareMemory >= (application.memory * 1.2),
                     spareStorage >= (application.disk * 1.5) )
        forall(Application(canCoExist == true) from instance.applications)
    then
        insert(new Assignment(application, instance))
end

rule "Can colocate, but no instance available"
    when
        application: Application(canCoExist == true)
        not Instance(numberOfApps < 4)
    then
        insert(new NewInstanceNeeded(application))
end


rule "Still nothing found, create an instance anyway"
    salience -200
    when
        as: Application()
    then
        insert(new NewInstanceNeeded(application))
end

rule "Collect assigment or instance requests to return as advice"
    salience 1000
    when
        ap: Application()
        as: (Assignment(application == ap) or NewInstanceNeeded(application == ap))
    then
        results.add(as)
        retract(ap)
end




















